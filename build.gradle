buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'forge'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.configFile = file "build.properties"
configFile.withReader {
    def buildProperties = new Properties()
    buildProperties.load(it)
    project.ext.config = new ConfigSlurper().parse(buildProperties)
}

version = "${config.minecraftVersion}-${config.modVersion}-${config.buildNumber}"
group = config.modGroup
description = config.modDescription
archivesBaseName = config.modNameShort

minecraft {
    version = "${config.minecraftVersion}-${config.forgeVersion}"
    runDir = "minecraft"

    replaceIn "reference/Reference.java"
    replace "@VERSION@", "${config.minecraftVersion}-${config.modVersion}-${config.buildNumber}"
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'modid', project.config.modNameShort
  	inputs.property 'name', project.config.modNameLong
 	inputs.property 'description', project.config.modDescription
 	inputs.property 'author', project.config.modAuthor
  
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand([ 
        	'version':project.version, 
        	'mcversion':project.minecraft.version,
        	'modid':project.config.modNameShort,	
        	'name':project.config.modNameLong,
  			'description':project.config.modDescription,
  			'author':project.config.modAuthor
  		])
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    dependsOn "updateBuildInformation"
	classifier = 'universal'
    manifest.mainAttributes(
            "Built-By": project.config.modAuthor,
            "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
    )
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn sourceJar, javadocJar, deobfJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

task updateBuildInformation {
	// increment build number
	doFirst {
		// increment
		config.buildNumber = (config.buildNumber.toString().toInteger()) + 1
		config.buildDate = new Date().format("yyyyMMdd'T'HHmmss")
		// write back to the file
		configFile.withWriter {
			config.toProperties().store(it, "")
		}
	}
}
